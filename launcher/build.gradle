apply plugin: "com.github.johnrengelman.shadow"

description "Launcher for the Loom server."

buildscript {
    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.github.malensek:jbsdiff:$jbsdiffVersion"
    }
}

dependencies {
    implementation "com.github.malensek:jbsdiff:$jbsdiffVersion"
    compileOnly project(":loom-server")
}

def dataFolder = new File(project.buildDir.getPath(), "data")

// https://stackoverflow.com/a/51660202

task generateData {
    doLast {
        if (!dataFolder.exists()) {
            dataFolder.mkdirs();
        }

        def propertiesFile = new File(dataFolder.getPath(), "launcher.properties")
        def patchFile = new File(dataFolder.getPath(), "loom.patch")
        
        def vanillaJar = rootProject.file(".cache" + File.separator + project.mcVersion + File.separator + "server.jar")
        def vanillaSha1 = vanillaJar.bytes.digest("SHA-1")

        def patchedJar = project(":loom-server").jar.archivePath
        def patchedSha1 = patchedJar.bytes.digest("SHA-1")

        Properties properties = new Properties()
        properties.put("vanilla.download", "https://launcher.mojang.com/v1/objects/$vanillaSha1/server.jar".toString())
        properties.put("vanilla.sha1", vanillaSha1)
        properties.put("patched.sha1", patchedSha1)
        propertiesFile.withWriter { out ->
            properties.store(out, "Loom Launcher Properties".toString())
        }
        project.javaexec({ spec ->
            spec.classpath(project.buildscript.configurations.classpath)
            spec.setMain("io.sigpipe.jbsdiff.ui.CLI")
            spec.jvmArgs("-Xmx512M")
            spec.setErrorOutput(System.err)
            spec.setArgs([
                "diff",
                vanillaJar,
                patchedJar,
                patchFile
            ])
        })
    }
}

// https://stackoverflow.com/a/48249897

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    classifier = null
    dependsOn(generateData)
    from dataFolder
    manifest {
        attributes(
            "Main-Class": "org.loomdev.launcher.LoomLauncher", 
            "Launcher-Agent-Class": "org.loomdev.launcher.Agent",
            "Premain-Class": "org.loomdev.launcher.Agent"
        )
    }
}
